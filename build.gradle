plugins {
    id 'java'
    id 'idea'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

ext {
    minecraftVersion = "1.20.1"
    forgeVersion = "47.1.3"
    mixinVersion = "0.8.5"
    modVersion = "1.0.1"
    jeiVersion = "15.2.0.21"
    patchouliVersion = "1.20.1-81-FORGE"
    jadeVersion = "4614153"
    topVersion = "4629624"
    tfcVersion = "5872631"
    modId = "placeableingots"
}

base {
    archivesName = "placeableingots"
    group = "fr.iglee42"
    version = project.ext.modVersion
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

idea {
    module {
        excludeDirs.add(file("run"))
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://dvs1.progwml6.com/files/maven/" } // JEI
    maven { url "https://modmaven.k-4u.nl" } // Mirror for JEI
    maven { url "https://maven.blamejared.com" } // Patchouli
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.ext.minecraftVersion}-${project.ext.forgeVersion}"
    // TFC
    implementation fg.deobf("curse.maven:tfc-302973:${project.ext.tfcVersion}")

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-${project.ext.minecraftVersion}-forge-api:${project.ext.jeiVersion}")
    compileOnly fg.deobf("mezz.jei:jei-${project.ext.minecraftVersion}-common-api:${project.ext.jeiVersion}")
    runtimeOnly fg.deobf("mezz.jei:jei-${project.ext.minecraftVersion}-forge:${project.ext.jeiVersion}")

    // Patchouli
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${project.ext.patchouliVersion}")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${project.ext.patchouliVersion}")

    // Jade / The One Probe
    compileOnly fg.deobf("curse.maven:jade-324717:${project.ext.jadeVersion}")
    compileOnly fg.deobf("curse.maven:top-245211:${project.ext.topVersion}")

    // Only use Jade at runtime
    runtimeOnly fg.deobf("curse.maven:jade-324717:${project.ext.jadeVersion}")
}

minecraft {
    mappings "parchment", "2023.09.03-1.20.1"
    accessTransformer file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        all {
            property "forge.logging.console.level", "debug"

            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${project.projectDir}/build/createSrgToMcp/output.srg"

            jvmArgs "-ea", "-Xmx4G", "-Xms4G"

            mods.create(project.ext.modId) {
                source sourceSets.main
            }
        }

        client {
            workingDirectory project.file("run/client")
        }

        server {
            workingDirectory project.file("run/server")
            arg "--nogui"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.jar {
    manifest {
        attributes["Implementation-Version"] = project.version
    }
}

tasks.withType(ProcessResources).configureEach {
    inputs.property("version", project.version)
    filesMatching("META-INF/mods.toml") {
        expand(
            mod_id: project.ext.modId,
            mod_version: project.version,
            loader_version_range: "[47,)",
            mod_license: "MIT",
            mod_name: "PlaceableIngots",
            mod_authors: "iglee42",
            mod_description: "",
            forge_version_range: "[${project.ext.forgeVersion},)",
            minecraft_version_range: "[${project.ext.minecraftVersion},)"
        )
    }
}
